//file:noinspection GradlePackageVersionRange
plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

allprojects {
    apply plugin: "java"
    apply plugin: "fabric-loom"
    apply plugin: "maven-publish"

    def ENV = System.getenv()

    version = "${project.mod_version}+${rootProject.minecraft_base_version}"
    group = rootProject.maven_group

    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        jcenter()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    }

    processResources {
        inputs.property "version", project.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    def targetJavaVersion = 21
    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        // If Javadoc is generated, this must be specified in that task too.
        it.options.encoding = "UTF-8"
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            it.options.release = targetJavaVersion
        }

        if (JavaVersion.current() != JavaVersion.VERSION_1_8 && sourceSets.main.allJava.files.any {it.name == "module-info.java"}) {
            options.compilerArgs += ['-Xplugin:Manifold', '--module-path', it.classpath.asPath]
        } else {
            options.compilerArgs += ['-Xplugin:Manifold']
        }
    }

    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
        archivesBaseName = project.archives_base_name
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}" }
        }

        manifest {
            attributes('Contains-Sources':'java,class')
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USER
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}

repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://maven.shedaniel.me/" }
    maven {
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven { url "https://maven.nucleoid.xyz/" }
    maven { url 'https://maven.wispforest.io' }
    mavenLocal()
}

sourceSets {
    testmod {
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.compileClasspath
    }
}

loom {
    runs {
        testmodClient {
            client()
            ideConfigGenerated project.rootProject == project
            name = "Testmod Client"
            source sourceSets.testmod
        }
        testmodServer {
            server()
            ideConfigGenerated project.rootProject == project
            name = "Testmod Server"
            source sourceSets.testmod
        }
    }

    accessWidenerPath = file("src/main/resources/owo.accesswidener")
}

dependencies {
//    modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin:${project.rei_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}")

    modCompileOnly("dev.emi:emi-fabric:${project.emi_version}")
//    modLocalRuntime("dev.emi:emi-fabric:${project.emi_version}")

    modCompileOnly("com.terraformersmc:modmenu:${project.modmenu_version}")
    modLocalRuntime("com.terraformersmc:modmenu:${project.modmenu_version}")

    include api("io.wispforest:endec:0.1.0")
    include api("io.wispforest.endec:netty:0.1.0")
    include api("io.wispforest.endec:gson:0.1.0")
    include api("io.wispforest.endec:jankson:0.1.0")

    include api("blue.endless:jankson:${project.jankson_version}")

    implementation testmodImplementation("systems.manifold:manifold-ext-rt:2024.1.17")
    annotationProcessor testmodAnnotationProcessor("systems.manifold:manifold-ext:2024.1.17") // Add manifold to -processorpath for javac

    modCompileOnly("xyz.nucleoid:server-translations-api:${project.stapi_version}")

    testmodImplementation sourceSets.main.output
    testmodAnnotationProcessor sourceSets.main.output
}

javadoc {
    options.stylesheetFile = new File(projectDir, "stylesheet.css")
    options.tags = ["apiNote", "implNote", "implSpec"]
    options.addStringOption("Xdoclint:-missing", "-quiet")
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes('Contains-Sources':'java,class')
    }
}
